apply plugin: 'com.android.library'

dependencies {
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'com.squareup.okhttp3:okhttp:3.3.1'
    compile 'com.android.support:support-annotations:24.0.0'
}

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.0"

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 24
        testApplicationId "com.commonsware.cwac.netseccfg.test"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "TEST_SERVER_HOST", '"'+TEST_SERVER_HOST+'"'
    }
}

// from http://stackoverflow.com/a/19484146/115145

android.libraryVariants.all { variant ->
  def name = variant.buildType.name
  if (name.equals(com.android.builder.core.BuilderConstants.DEBUG)) {
    return; // Skip debug builds.
  }
  def task = project.tasks.create "jar${name.capitalize()}", Jar
  task.dependsOn variant.javaCompile
  task.from variant.javaCompile.destinationDir
  task.baseName = "cwac-${PUBLISH_ARTIFACT_ID}"
  task.version = PUBLISH_VERSION
  task.exclude('com/commonsware/cwac/**/BuildConfig.**')
}

apply plugin: 'maven'
 
uploadArchives {
    repositories.mavenDeployer {
        pom.groupId = PUBLISH_GROUP_ID
        pom.artifactId = PUBLISH_ARTIFACT_ID
        pom.version = PUBLISH_VERSION

        repository(url: LOCAL_REPO)
    }
}

